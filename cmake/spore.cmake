function(spore_codegen SPORE_TARGET_NAME)
  cmake_parse_arguments(
    "SPORE_CODEGEN"
    "DEBUG;SEQUENTIAL;FORCE;REFORMAT"
    "CONFIG;CACHE;CXX_STANDARD;TARGET_NAME;BIN_NAME"
    "DEFINITIONS;INCLUDES;FEATURES"
    ${ARGN}
  )

  if (NOT SPORE_CODEGEN_BIN_NAME)
    set(SPORE_CODEGEN_BIN_NAME "spore.codegen.bin")
  endif()

  if (NOT SPORE_CODEGEN_TARGET_NAME)
    set(SPORE_CODEGEN_TARGET_NAME "${SPORE_TARGET_NAME}.codegen")
  endif()

  if (NOT SPORE_CODEGEN_CXX_STANDARD)
    set(SPORE_CODEGEN_CXX_STANDARD $<TARGET_PROPERTY:${SPORE_TARGET_NAME},CXX_STANDARD>)
  endif()

  list(
    APPEND SPORE_CODEGEN_INCLUDES
      $<TARGET_PROPERTY:${SPORE_TARGET_NAME},INCLUDE_DIRECTORIES>
      $<TARGET_PROPERTY:${SPORE_TARGET_NAME},INTERFACE_INCLUDE_DIRECTORIES>
  )

  list(
    APPEND SPORE_CODEGEN_DEFINITIONS
      $<TARGET_PROPERTY:${SPORE_TARGET_NAME},COMPILE_DEFINITIONS>
      $<TARGET_PROPERTY:${SPORE_TARGET_NAME},INTERFACE_COMPILE_DEFINITIONS>
  )

  list(
    APPEND SPORE_CODEGEN_FEATURES
      $<TARGET_PROPERTY:${SPORE_TARGET_NAME},COMPILE_FEATURES>
      $<TARGET_PROPERTY:${SPORE_TARGET_NAME},INTERFACE_COMPILE_FEATURES>
  )

  add_custom_target(
    ${SPORE_CODEGEN_TARGET_NAME}
    COMMAND
      $<TARGET_FILE:${SPORE_CODEGEN_BIN_NAME}>
        "$<$<BOOL:${SPORE_CODEGEN_CONFIG}>:--config;${SPORE_CODEGEN_CONFIG};>"
        "$<$<BOOL:${SPORE_CODEGEN_CACHE}>:--cache;${SPORE_CODEGEN_CACHE};>"
        "$<$<BOOL:${SPORE_CODEGEN_CXX_STANDARD}>:--cpp-standard;${SPORE_CODEGEN_CXX_STANDARD};>"
        "$<$<BOOL:$<FILTER:${SPORE_CODEGEN_INCLUDES},EXCLUDE,^$>>:--includes;$<JOIN:${SPORE_CODEGEN_INCLUDES},;--includes;>>"
        "$<$<BOOL:$<FILTER:${SPORE_CODEGEN_DEFINITIONS},EXCLUDE,^$>>:--definitions;$<JOIN:${SPORE_CODEGEN_DEFINITIONS},;--definitions;>>"
        "$<$<BOOL:$<FILTER:${SPORE_CODEGEN_FEATURES},EXCLUDE,^$>>:--features;$<JOIN:${SPORE_CODEGEN_FEATURES},;--features;>>"
        "$<$<BOOL:${SPORE_CODEGEN_DEBUG}>:--debug>"
        "$<$<BOOL:${SPORE_CODEGEN_FORCE}>:--force>"
        "$<$<BOOL:${SPORE_CODEGEN_REFORMAT}>:--reformat>"
        "$<$<BOOL:${SPORE_CODEGEN_SEQUENTIAL}>:--sequential>"
    COMMENT "Running codegen for ${SPORE_TARGET_NAME} in ${SPORE_CODEGEN_OUTPUT}"
    WORKING_DIRECTORY $<TARGET_PROPERTY:${SPORE_TARGET_NAME},SOURCE_DIR>
    COMMAND_EXPAND_LISTS
    VERBATIM
  )

  add_dependencies(
    ${SPORE_TARGET_NAME}
    ${SPORE_CODEGEN_TARGET_NAME}
  )
endfunction()
